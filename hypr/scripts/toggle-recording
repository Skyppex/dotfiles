#!/usr/bin/env nu

const pid_file = "/tmp/hypr/wf-recording.pid"
const out = "/tmp/hypr/recording.mp4"
const thumbnail = "/tmp/hypr/recording-thumb.png"

def is-alive []: nothing -> bool {
    if not ($pid_file | path exists) {
        return false
    }

    let pid = (open -r $pid_file | str trim | into int)

    if (ps | where pid == $pid | is-empty) {
        return false
    }

    return true
}

mkdir "/tmp/hypr"

if (is-alive) {
    let pid = (open -r $pid_file | str trim | into int)
    kill --signal 2 $pid
    rm $pid_file
    $"file://($out)(char --unicode d)(char --unicode a)" | wl-copy --type text/uri-list
    ffmpeg -y -hide_banner -loglevel error -i $out -vf "thumbnail" -frames:v 1 $thumbnail
    notify-send --icon $thumbnail "recording stopped"
} else {
    let region = slurp

    if ($out | path exists) {
        rm $out
    }

    let command = $"wf-recorder --audio-backend=pipewire --geometry '($region)' --audio --file ($out) >/dev/null 2>&1 & echo $!"
    let last_pid = (^bash -c $command | into int)

    $last_pid | save --force $pid_file
    notify-send "recording started"
}
