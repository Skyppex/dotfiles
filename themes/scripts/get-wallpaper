#!/usr/bin/env nu

def main [
    --config-option: string
    direction: string
    purpose?: string
] {
    let themes = "~/.config/themes" | path expand
    let current = open ($themes | path join "current.toml")
    let theme = $themes | path join $current.dir
    let images = $theme | path join "images"
    let config = open ($theme | path join "config.toml")
    let variant = if ($config_option | is-not-empty) {
        $config | get $config_option
    } else {
        $config.variant
    }

    let file = ls $images 
    | get name 
    | path basename 
    | each { |it|
        let index_colon = $it | str index-of "_"
        let index_dot = $it | str index-of "."
        let tags = $it 
        | str substring ($index_colon + 1)..($index_dot - 1) 
        | split row "-"
        { file: $it, tags: $tags }
    }
    | where { |it| 
        match $it.tags {
            [$v, $d, $p] => (($v == $variant) and ($d == $direction) and ($p == $purpose)),
            [$v, $d] => (($v == $variant) and ($d == $direction)),
            _ => false
        }
    }
    | get file
    | first

    let full_path = $images | path join $file | path expand

    if ($full_path | str starts-with $env.HOME) {
        $full_path | str replace $env.HOME "~"
    } else {
        $full_path
    }
}
